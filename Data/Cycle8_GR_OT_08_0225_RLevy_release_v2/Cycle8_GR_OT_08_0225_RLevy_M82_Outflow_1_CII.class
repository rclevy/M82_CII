!===========================================================================
! (1)   OPEN THE DATA IN MAIN BEAM TEMPERATURES, SELECT THE ON-OFF OBSERVATIONS 
!	OF THE OUTFLOW 1 POSITION, SMOOTH TO A 10 KM/S VELOCITY RESOLTUION AND 
!	SUBTRACT A LINEAR BASELINE
!
!	Note: this project was observed during multiple flights and mixed
!	      on-off observations and on-the-flight maps. Thus the selection of 
! 	      outflow_1 observations is done by its respective scans. To see
! 	      which scans correspond to which position and observing mode, please
!	      check the flight logs.
!	Note: there were two slightly different tuning frequencies during
!	      the flights. To correct for this, a common M82 velocity of
!	      250 km/s is assumed for all data.
!===========================================================================

file in Cycle8_GR_OT_08_0225_RLevy_Tmb.great

$rm -f Cycle8_GR_OT_08_0225_RLevy_M82_Outflow_1_CII_temp.great
file out Cycle8_GR_OT_08_0225_RLevy_M82_Outflow_1_CII_temp.great s /o

set mode x -150 420		! To avoid bad channels at the edges of the bandpass
set window -80 380		
set weight sigma

!=========================================================

find /scan 36748 36752		! Fligth HOGAN on February 19, 2021
	
for i 1 to found
	get n
	modify velo 250.0
	smooth box 26
	smooth box 10
	base 1 
	write
next

!=========================================================

find /scan 36876 36889		! Fligth HAZEL on February 23, 2021

for i 1 to found
	get n
	modify velo 250.0
	smooth box 26
	smooth box 10
	base 1 
	write
next

!=========================================================

find /scan 37164 37170		! Fligth HELEN on February 25, 2021

for i 1 to found
	get n
	modify velo 250.0
	smooth box 26
	smooth box 10
	base 1 
	write
next

!=========================================================

find /scan 37751 37767		! Fligth HOWARD on March 10, 2021

for i 1 to found
	get n
	modify velo 250.0
	smooth box 26
	smooth box 10
	base 1 
	write
next

!=========================================================

find /scan 37885 37917		! Fligth HERA on March 11, 2021

for i 1 to found
	get n
	modify velo 250.0
	smooth box 26
	smooth box 10
	base 1 
	write
next



!===========================================================================
! (2) AVERAGE SPECTRA PER POSITION 
!     Note: the average has to be done by position and not by pixel,
!	    because the array was rotated during the observations
!     Note: to write down the rms associated to each pixel an additional
!	    baseline of order 0 is applied
!     Note: for some positions there are bad spectra that have to be excluded
!===========================================================================

set match 10

file in Cycle8_GR_OT_08_0225_RLevy_M82_Outflow_1_CII_temp.great 

$rm -f Cycle8_GR_OT_08_0225_RLevy_M82_Outflow_1_CII.great 
file out Cycle8_GR_OT_08_0225_RLevy_M82_Outflow_1_CII.great  s /o


!===================

find /offset 30 -80		! THIS IS THE CENTRAL POSITION OF THE ARRAY

ave /res /nocheck cal
pl
base 0
write

!===================

find /offset 10 -105
ave /res /nocheck cal
pl
base 0
write

!===================

find /offset 40 -110		!Here we have to exclude bad spectra from pixel LFAV-5 

$rm -f Outflow_1_offset_40_-110.great
file out Outflow_1_offset_40_-110.great s /o

find /offset 40 -110 /tel SOF-LFA*_2_S
for i 1 to found
	get n
	write
next 

find /offset 40 -110 /tel SOF-LFA*_3_S
for i 1 to found
	get n
	write
next 

find /offset 40 -110 /tel SOF-LFAH_5_S
for i 1 to found
	get n
	write
next 

set tel *


file out Cycle8_GR_OT_08_0225_RLevy_M82_Outflow_1_CII.great
file in Outflow_1_offset_40_-110.great

find /all

ave /res /nocheck cal
pl
base 0
write

!================

file in Cycle8_GR_OT_08_0225_RLevy_M82_Outflow_1_CII_temp.great 

find /offset 60 -85
ave /res /nocheck cal
pl
base 0
write

!================

find /offset 50 -55
ave /res /nocheck cal
pl
base 0
write

!================

find /offset 15 -52
ave /res /nocheck cal
pl
base 0
write

!================

find /offset -5 -75 		! Here we have to exclude the bad spectra from pixel LFAV-5 

$rm -f Outflow_1_offset_-5_-75.great
file out Outflow_1_offset_-5_-75.great s /o

find /offset -5 -75 /tel SOF-LFA*_1_S
for i 1 to found
	get n
	write
next 

find /offset -5 -75 /tel SOF-LFA*_4_S
for i 1 to found
	get n
	write
next 


find /offset -5 -75 /tel SOF-LFAH_5_S
for i 1 to found
	get n
	write
next 

set tel *

file out Cycle8_GR_OT_08_0225_RLevy_M82_Outflow_1_CII.great
file in Outflow_1_offset_-5_-75.great

find /all

ave /res /nocheck cal
pl
base 0
write



!===========================================================================
! (4) OPEN THE FINAL FILE AND CHECK THE RESULTS
!===========================================================================

file in Cycle8_GR_OT_08_0225_RLevy_M82_Outflow_1_CII.great
find /all

set mode y t

stamp

set char 0.5

draw text 3.5 5.5 "(+28'',-82'')"
draw text 8.0 5.5 "rms= 8 mK"

draw text 11.5 5.5 "(+10'',-106'') "
draw text 16 5.5 "rms= 7 mK"

draw text 19 5.5 "(+38'',-107'')"
draw text 24 5.5 "rms= 7 mK"

draw text 3.5 10 "(+63'',-86'')"
draw text 8 10 "rms= 10 mK"

draw text 11.5 10 "(+52'',-59'')"
draw text 16 10 "rms= 10 mK"

draw text 19 10 "(+20'',-54'')"
draw text 24 10 "rms= 11 mK"

draw text 3.5 14 "(-3'',-76'')"
draw text 8.0 14 "rms= 12 mK"

set char 1
draw text 12 13 "M82 - [CII] - Outflow 1 position"

$rm -f Cycle8_GR_OT_08_0225_RLevy_M82_Outflow_1_CII.png
hardcopy Cycle8_GR_OT_08_0225_RLevy_M82_Outflow_1_CII /device png color


!===============================================
! DELETE INTERMEDIATE FILES (not needed anymore)
!===============================================

$rm -f Cycle8_GR_OT_08_0225_RLevy_M82_Outflow_1_CII_temp.great 	
$rm -f Outflow_1_offset_40_-110.great
$rm -f Outflow_1_offset_-5_-75.great


